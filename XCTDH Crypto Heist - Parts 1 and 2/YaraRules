Part 1: 
These detection rules operate exclusively at the filesystem level to identify weaponised repositories within your environment:

rule DPRKObfuscatedJavaScript1 {
    meta:
        description = "RepoCrossChainTxDataHiding detection with specific + generic indicators"
        author = "Ransom-ISAC"

    strings:
        // High-confidence specific strings
        $s1 = "global['_V']"
        $s2 = "global['r']"

        // Generic obfuscation patterns
        $obf1 = ".charAt(" nocase
        $obf2 = ".substr(" nocase
        $obf3 = /function \w{3}\(\w\)\{/

        // Suspicious execution patterns
        $exec1 = "require"
        $exec2 = /\(\)\)\(\)/

    condition:
        filesize < 50KB and
        all of ($s*) and
        2 of ($obf*) and
        1 of ($exec*)
}
If this doesn't provide any results for safe keeping, you can run the following; however, be wary of potential false positives:

rule DPRKObfuscatedJavaScript2 {
    meta:
        description = "Flexible RepoCrossChainTxDataHiding detection"
        author = "Ransom-ISAC"

    strings:
        $s1 = "global['_V']"
        $s2 = "global['r']"
        $obf = ".charAt" nocase
        $req = "require"

    condition:
        filesize < 100KB and
        all of them
}

Part 2: 
Rule 1: Actor_APT_DPRK_Unknown_MAL_Script_PY_Stealer_Unknown_Strings_1_1Oct25
rule Actor_APT_DPRK_Unknown_MAL_Script_PY_Stealer_Unknown_Strings_1_1Oct25
{
      meta:
            rule_id = "7919137c-de06-43cc-800a-76c726b45fbd"
            date = "16-10-2025"
            author = "Ransom-ISAC"
            //Payload 1_2_1_1 OmniStealer
            description = "Detects cluster of Python Scripts that are likely developed by a DPRK Nexus group"
            filehash = "742016f01fa89be4d43916d5d2349c8d86dc89f096302501ec22b5c239685a20"

      strings:
            $bwr1 = "microsoft-edge" ascii
            $bwr2 = "google-chrome" ascii
            $bwr3 = "Brave-Browser" ascii

            $func1 = "socket.gethostname()" ascii
            $func2 = "getpass.getuser()" ascii
            $func3 = "platform.platform()" ascii

            $str1 = "1Password" ascii
            $str2 = "secretstorage" ascii
            $str3 = "networkWallet" ascii
            $str4 = "readPassword" ascii
            $str5 = "cookie_files" ascii
            $str6 = "login_files" ascii
            $str7 = "credit_cards" ascii
            $str8 = "masterPassword" ascii
            $str9 = "moz_cookies" ascii
            $str10 = "http-upload" ascii
            $str11 = "tg-upload" ascii

            $pass1 = "ProtonPass" ascii
            $pass2  = "MEGAPass" ascii
            $pass3  = "DualSafe" ascii
            $pass4  = "FreePasswordManager" ascii
            $pass5  = "GoogleAuth" ascii

            $params1 = "osx_key_user" ascii
            $params2 = "osx_key_service" ascii
            $params3 = "os_crypt_name" ascii
            $params4 = "windows_keys" ascii
            $params5 = "osx_cookies" ascii
            $params6 = "windows_cookies" ascii
            $params7 = "linux_cookies" ascii
            $params8 = "osx_logins" ascii
            $params9 = "windows_logins" ascii
            $params10 = "linux_logins" ascii

            $crpt1 = "Bitwarden" ascii
            $crpt2 = "NordPass" ascii
            $crpt3 = "Dashlane" ascii
            $crpt4 = "kwallet" ascii

            $pths1 = "/.config/chromium/" ascii
            $pths2 = "/.config/opera/" ascii
            $pths3 = "/.config/BraveSoftware/" ascii
            $pths4 = "/.config/microsoft-edge" ascii
            $pths5 = "/.config/vivaldi/" ascii
            $pths6 = "%APPDATA%\\\\*\\\\*\\\\*\\\\User Data*" ascii

            $walls1 = "Dogecoin/wallets.dat" ascii
            $walls2 = "Bitcoin/wallets" ascii
            $walls3 = "Electrum/wallets" ascii
            $walls4 = "Exodus/exodus.wallet" ascii
            $walls5 = "Monero/wallets" ascii

            $drv1 = "iCloud Drive" ascii
            $drv2 = "SkyDrive" ascii
            $drv3 = "OneDrive" ascii
            $drv4 = "My Drive" ascii
            $drv5 = "Dropbox" ascii
            $drv6 = "pCloud" ascii
            $drv7 = "Box" ascii
            $drv8 = "iCloud" ascii
            $drv9 = "SkyDrive" ascii
            $drv10 = "GoogleDrive" ascii
            $drv11 = "Dropbox" ascii
            $drv12 = "Mega" ascii

      condition:
            any of ($bwr*)
            and any of ($func*)
            and 5 of ($str*)
            and 2 of ($pass*)
            and 5 of ($params*)
            and 2 of ($crpt*)
            and 3 of ($pths*)
            and 2 of ($walls*)
            and 6 of ($drv*)
            and filesize < 250KB

      /*------------------------Matches = 2---------------------------
    742016f01fa89be4d43916d5d2349c8d86dc89f096302501ec22b5c239685a20  ---Communicating across found C2 infra
    a7d7075e866132b8e8eb87265f7b7fab0e9f6dd7f748445a18f37da2e989faa3  ---Communicating across found C2 infra
    */
}
Rule 2: Actor_APT_DPRK_Unknown_MAL_Script_PY_Stealer_Unknown_Strings_2_Oct25
rule Actor_APT_DPRK_Unknown_MAL_Script_PY_Stealer_Unknown_Strings_2_Oct25
{
      meta:
            rule_id = "2c2a60ce-55cf-40ab-92c4-7ee961b0d00c"
            date = "17-10-2025"
            author = "Ransom-ISAC"
            //Payload 1_2_1_1 OmniStealer
            description = "Detects cluster of Python Scripts that are likely developed by a DPRK Nexus group"
            filehash = "236ff897dee7d21319482cd67815bd22391523e37e0452fa230813b30884a86f"

      strings:
            $dot1 = ".onetoc2" ascii
            $dot2 = ".onenote" ascii
            $dot3 = ".one" ascii
            $dot4 = ".kbdx" ascii

            $func1 = "socket.gethostname()" ascii
            $func2 = "getpass.getuser()" ascii
            $func3 = "platform.platform()" ascii

            $pc1 = "pc_name" ascii
            $pc2 = "pc_info" ascii
            $pc3 = "pc_login" ascii

            $x1 = "metamask" ascii
            $x2 = "phantom" ascii
            $x3 = "exodus" ascii
            $x4 = "atomic" ascii
            $x5 = "bitcoin" ascii
            $x6 = "ethereum" ascii
            $x7 = "solana" ascii
            $x8 = "aptos" ascii
            $x9 = "electrum" ascii
            $x10 = "tronlin" ascii
            $x11 = "coinbase" ascii
            $x12 = "binance" ascii

            $y1 = "gitconfig" ascii
            $y2 = "tsconfig" ascii
            $y3 = "bootconfig" ascii
            $y4 = "pw-config" ascii

            $z1 = "cli_mode" ascii
            $z2 = "dev_mode" ascii
            $z3 = "cli_mode" ascii
            $z4 = "debug_mode" ascii

      condition:
            2 of ($dot*)
            and any of ($func*)
            and any of ($pc*)
            and 6 of ($x*)
            and 2 of ($y*)
            and 2 of ($z*)
            and filesize < 100KB
}
Rule 3: Actor_APT_DPRK_Unknown_MAL_Script_JS_Loader_Unknown_Strings_Oct25
rule Actor_APT_DPRK_Unknown_MAL_Script_JS_Loader_Unknown_Strings_Oct25
{
      meta:
            rule_id = "dbcf26b3-7b8c-447d-97ad-43de0d6e42e6"
            date = "17-10-2025"
            author = "Ransom-ISAC"
            description = "Detects cluster of JS Scripts that are likely developed by a DPRK Nexus group"
            filehash = "be21bf4ad94c394202e7b52a1b461ed868200f0f03b3c8544984e9765c23e1e0"

      strings:
            $hex = {676c6f62616c2e5f56203d202743352d62656e6566697427} //global._V = 'C5-benefit'

            $js1 = "global.r" ascii
            $js2 = "global._V" ascii

            $var1 = "C5-benefit" ascii
            $var2 = "C250617A" ascii
            $var3 = "CHQG3L42MMQ" ascii
            $var4 = {68 74 74 70 3a 2f 2f 22 20 2b 20 ?? 20 2b 20 22 3a (32 37 30 31 37 | 44 44 43)} //IP:Port pattern

            $str1 = "crypto" ascii
            $str2 = "socket" ascii
            $str3 = "hostname" ascii
            $str4 = "axios" ascii
            $str5 = "form-data" ascii

            condition:
                  $hex
                  or (
                        any of ($js*)
                        and any of ($var*)
                        and any of ($str*)
                  )
                  and filesize < 75KB
}
Rule 4: Actor_APT_DPRK_Unknown_MAL_Script_JS_RAT_Unknown_Strings_Oct25
rule Actor_APT_DPRK_Unknown_MAL_Script_JS_RAT_Unknown_Strings_Oct25
{
      meta:
            rule_id = "96fd2b7e-355e-43fc-a581-6ebda388b761"
            date = "19-10-2025"
            author = "Ransom-ISAC"
            //Payload1_1_1 Cross-Platfrom NodeJS RAT
            description = "Detects cluster of obfuscated JS Scripts that are likely developed by a DPRK Nexus group"
            filehash = "eefe39fe88e75b37babb37c7379d1ec61b187a9677ee5d0c867d13ccb0e31e30"

      strings:
            $str1 = "Promise" ascii wide
            $str2 = "['_V']" ascii wide
            $str3 = "['_R']" ascii wide
            $str4 = "atob" ascii wide

            condition:
                all of them
                and filesize < 100KB
}
Rule 5: Actor_APT_DPRK_Unknown_MAL_Indicators_Strings_Oct25
rule Actor_APT_DPRK_Unknown_MAL_Indicators_Strings_Oct25
{
      meta:
            rule_id = "10982aed-1c45-4864-a6ff-ffd19f38912d"
            date = "19-10-2025"
            author = "Ransom-ISAC"
            description = "Detects cluster of DPRK Nexus malware based on known artifacts"

      strings:
            $XOR1 = {32 5b 67 57 66 47 6a 3b 3c 3a 2d 39 33 5a 5e 43}
            $XOR2 = {6d 36 3a 74 54 68 5e 44 29 63 42 7a 3f 4e 4d 5d}
            $XOR3 = {63 41 5d 32 21 2b 33 37 76 2c 2d 73 7a 65 55 7d}
            $XOR4 = {54 68 5a 47 2b 30 6a 66 58 45 36 56 41 47 4f 4a}
            $XOR5 = {34 23 75 4c 65 56 4d 5b 33 6c 45 53 4c 47 41}
            $XOR6 = {39 4b 79 41 53 74 2b 37 44 30 6d 6a 50 48 46 59}
            $XOR7 = {54 68 5a 47 2b 30 6a 66 58 45 36 56 41 47 4f 4a}

            $tron1 = "TMfKQEd7TJJa5xNZJZ2Lep838vrzrs7mAP" ascii wide
            $tron2 = "TXfxHUet9pJVU1BgVkBAbrES4YUc1nGzcG" ascii wide
            $tron3 = "TLmj13VL4p6NQ7jpxz8d9uYY6FUKCYatS" ascii wide

            $aptos1 = "be037400670fbf1c32364f762975908dc43eeb38759263e7dfcdabc76380811e" ascii wide
            $aptos2 = "3f0e5781d0855fb460661ac63257376db1941b2bb522499e4757ecb3ebd5dce3" ascii wide
            $aptos3 = "3414a658f13b652f24301e986f9e0079ef506992472c1d5224180340d8105837" ascii wide

            $bsc1 = "f46c86c886bbf9915f4841a8c27b38c519fe3ce54ba69c98d233d0ffc94d19fc" ascii wide
            $bsc2 = "d33f78662df123adf2a178628980b605a0026c0d8c4f4e87e43e724cda258fef" ascii wide
            $bsc3 = "a8cdabea3616a6d43e0893322112f9dca05b7d2f88fd1b7370c33c79076216ff" ascii wide

            $telegram = "7870147428:AAGbYG_eYkiAziCKRmkiQF-" ascii wide

            $marker = "*C250617A*" ascii wide

            $obfs1 = "_$af402041" ascii wide
            $obfs2 = "_$af813180" ascii wide
            $obfs3 = "_$_2d00[]" ascii wide

      condition:
            any of them
}
